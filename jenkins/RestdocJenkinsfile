pipeline {
    agent none
    parameters {
        string(
            name: 'Tags',
            defaultValue: 'master',
            description: 'List of tags or "master" as a space delimited string for which to build rest api doc for (ex. enterprise-v1.x.y)'
        )
        choice(
            name: 'S3Bucket',
            choices: 'alluxio-restdoc-dev\nalluxio-restdoc',
            description: 'S3 bucket to upload rest api doc to'
        )
    }
    stages {
        stage('build') {
            agent {
                docker {
                    image 'agent'
                    label 'workerLarge'
                    alwaysPull false
                }
            }
            environment {
                AWS_ACCESS_KEY_ID     = credentials('JENKINS_AWS_ACCESS_KEY_ID')
                AWS_SECRET_ACCESS_KEY = credentials('JENKINS_AWS_SECRET_ACCESS_KEY')
            }
            steps {
                script {
                    def folderName = "ee"
                    def edition = "enterprise"
                    
                    for (tag in params.Tags.split(" ")) {
                        def branch = "refs/tags/${tag}"
                        if (tag == "master") {
                            branch = '*/master'
                        }
                        checkout([
                            $class: 'GitSCM',
                            branches: [[name: branch]],
                            userRemoteConfigs: [[
                                credentialsId: 'ALLUXIO-CI_GITHUB',
                                url: "https://github.com/TachyonNexus/${edition}",
                            ]]
                        ])
                        sh 'mvn clean install -DskipTests -Dmaven.javadoc.skip -Dcheckstyle.skip -Dlicense.skip -Dfindbugs.skip -Dannotation.skip -quiet'
                        sh "rm -rf restdoc"

                        // parse major.minor version from tag matching "v0.0" or leave version as is
                        def version = tag
                        def pattern = /.*v(\d+.\d+).*/
                        def matcher = (tag =~ pattern)
                        if (matcher) {
                            version = matcher[0][1]
                        } else {
                            echo "Tag did not match version regex pattern "+pattern+" -> "+tag
                            echo "Using given tag as folder name"
                        }
                        // find directories named miredot (ex. ./core/server/{proxy,master,worker}/target/miredot)
                        def miredots = sh (
                            script: 'find . -name "miredot" -type d',
                            returnStdout: true
                        )
                        miredots.split().each {
                            // extract component name in filepath (ex ./core/server/proxy/target/miredot -> proxy)
                            def component = it.tokenize("/")[3]
                            sh "mkdir -p restdoc/${version}/${component}"
                            // copy contents of miredot/* to restdoc/$version/$component
                            sh "cp -r ${it}/* restdoc/${version}/${component}"
                        }
                        sh "aws s3 sync restdoc/${version} s3://${params.S3Bucket}/${folderName}/${version}"
                    }
                }
            }
        }
    }
}

